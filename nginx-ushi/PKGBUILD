# Maintainer: ushi <ushi+arch@honkgong.info>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmal.com>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

_cfgdir=/etc/nginx
_tmpdir=/var/tmp/nginx

_authpamname='ngx_http_auth_pam_module'
_authpamver=1.5.1

pkgname=nginx-ushi
pkgver=1.11.0
pkgrel=1
pkgdesc="lightweight HTTP server and IMAP/POP3 proxy server"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
url="http://nginx.org"
license=('custom')
provides=('nginx')
conflicts=('nginx')

backup=(
  "${_cfgdir:1}/fastcgi.conf"
  "${_cfgdir:1}/fastcgi_params"
  "${_cfgdir:1}/koi-win"
  "${_cfgdir:1}/koi-utf"
  "${_cfgdir:1}/mime.types"
  "${_cfgdir:1}/nginx.conf"
  "${_cfgdir:1}/scgi_params"
  "${_cfgdir:1}/uwsgi_params"
  "${_cfgdir:1}/win-utf"
  'etc/logrotate.d/nginx'
)

source=(
  "http://nginx.org/download/nginx-${pkgver}.tar.gz"
  "https://github.com/stogh/${_authpamname}/archive/v${_authpamver}.tar.gz"
  'logrotate'
  'service'
)

sha256sums=(
  '6ca0e7bf540cdae387ce9470568c2c3a826bc7e7f12def1ae7d20b66f4065a99'
  '77676842919134af88a7b4bfca4470223e3a00d287d17c0dbdc9a114a685b6e7'
  'f88f25ce955b822099f7446c2751de2251feaa6a58e1fb5ec40678801b285e3b'
  '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
)

build() {
  cd "${srcdir}/nginx-${pkgver}"

  ./configure                                                       \
    --prefix="${_cfgdir}"                                           \
    --conf-path="${_cfgdir}/nginx.conf"                             \
    --sbin-path='/usr/bin/nginx'                                    \
    --pid-path='/var/run/nginx.pid'                                 \
    --lock-path='/var/lock/nginx.lock'                              \
    --user='http'                                                   \
    --group='http'                                                  \
    --http-log-path='/var/log/nginx/access.log'                     \
    --error-log-path='/var/log/nginx/error.log'                     \
    --http-client-body-temp-path="${_tmpdir}/client-body"           \
    --http-proxy-temp-path="${_tmpdir}/proxy"                       \
    --http-fastcgi-temp-path="${_tmpdir}/fastcgi"                   \
    --http-scgi-temp-path="${_tmpdir}/scgi"                         \
    --http-uwsgi-temp-path="${_tmpdir}/uwsgi"                       \
    --with-imap                                                     \
    --with-imap_ssl_module                                          \
    --with-ipv6                                                     \
    --with-pcre-jit                                                 \
    --with-file-aio                                                 \
    --with-threads                                                  \
    --with-http_dav_module                                          \
    --with-http_gzip_static_module                                  \
    --with-http_realip_module                                       \
    --with-http_ssl_module                                          \
    --with-http_stub_status_module                                  \
    --with-http_v2_module                                           \
    --with-http_mp4_module                                          \
    --add-module="${srcdir}/${_authpamname}-${_authpamver}"         \
    #--with-http_addition_module                                     \
    #--with-http_xslt_module                                         \
    #--with-http_image_filter_module                                 \
    #--with-http_geoip_module                                        \
    #--with-http_sub_module                                          \
    #--with-http_flv_module                                          \
    #--with-http_random_index_module                                 \
    #--with-http_secure_link_module                                  \
    #--with-http_degradation_module                                  \
    #--with-http_perl_module                                         \

  make
}

package() {
  cd "${srcdir}/nginx-${pkgver}"
  make DESTDIR="${pkgdir}" install

  sed -e 's|\<user\s\+\w\+;|user html;|g' -i "${pkgdir}/etc/nginx/nginx.conf"

  rm "${pkgdir}"/etc/nginx/*.default

  install -d "${pkgdir}/${_tmpdir}"
  install -dm0700 "${pkgdir}/${_tmpdir}/proxy"

  chmod 750 "${pkgdir}/var/log/nginx"
  chown http:log "${pkgdir}/var/log/nginx"

  install -d "${pkgdir}/usr/share/nginx"
  mv "${pkgdir}/etc/nginx/html/" "${pkgdir}/usr/share/nginx"

  install -Dm0644 "${srcdir}/logrotate" "${pkgdir}/etc/logrotate.d/nginx"
  install -Dm0644 "${srcdir}/service" "${pkgdir}/usr/lib/systemd/system/nginx.service"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/nginx/LICENSE"
  rm -rf "${pkgdir}/var/run"
}
