# Maintainer: ushi <ushi+arch@honkgong.info>
pkgname=facette
pkgver=0.3.0rc1
pkgrel=1
epoch=
pkgdesc="Time series data visualization and graphing software"
arch=('i686' 'x86_64')
url="https://facette.io/"
license=('BSD')
depends=('rrdtool')
makedepends=('pkg-config' 'go' 'nodejs' 'npm' 'pandoc-static')
backup=('etc/facette/facette.json')
install=facette.install

source=(
  "https://github.com/facette/facette/archive/${pkgver}.tar.gz"
  "${pkgname}.conf"
  "${pkgname}.install"
  "${pkgname}.service"
)

sha256sums=(
  '64387db2f3b4cc6bfdd9403e07f5f9c477eecf419539aa670de5d546d8c26876'
  '3502151e3cd7f5a49d4eedd6313fb28b987905e1fe6bd52359cfda8ddc0960b6'
  '6710ac12d676da1374bb522b36dcc79122624c7e342832377d3f358b5c65a3ce'
  '996246e98468a2848ef1c605ab49c569dc057299f847e166422595c5deda5b6f'
)

build() {
	cd "${pkgname}-${pkgver}"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make PREFIX="$pkgdir/usr" install

  # Replace /usr/local with /usr in config file
  sed -i 's#/usr/local#/usr#g' "docs/examples/facette.json"

  # Install config and systemd
  install -D -m644 "docs/examples/facette.json"             "${pkgdir}/etc/facette/facette.json"
  install -D -m644 "docs/examples/providers/collectd.json"  "${pkgdir}/etc/facette/providers/collectd.json"
  install -D -m644 "docs/examples/providers/graphite.json"  "${pkgdir}/etc/facette/providers/graphite.json"
  install -D -m644 "docs/examples/providers/influxdb.json"  "${pkgdir}/etc/facette/providers/influxdb.json"
  install -D -m644 "${srcdir}/facette.service"              "${pkgdir}/usr/lib/systemd/system/facette.service"
  install -D -m644 "${srcdir}/facette.conf"                 "${pkgdir}/usr/lib/tmpfiles.d/facette.conf"
}
